plugins {
    id 'idea'
    id 'java'
    id 'maven-publish'
    alias libs.plugins.archloom
}

base {
    archivesName = "${mod_id}-common-${libs.versions.minecraft.release.get()}"
}

configurations {
    commonJava {
        canBeResolved = false
        canBeConsumed = true
    }
    commonResources {
        canBeResolved = false
        canBeConsumed = true
    }
    testCompileOnly.extendsFrom(compileOnly)
}

loom {
    accessWidenerPath = file("src/main/resources/trimmed.accesswidener")

    mixin {
        defaultRefmapName = "trimmed.refmap.json"
    }

    runConfigs.configureEach {
        ideConfigGenerated = false
    }
}

dependencies {
    minecraft libs.minecraft
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${libs.versions.parchment.mc.get()}:${libs.versions.parchment.release.get()}@zip")
    }

    modCompileOnly libs.fabric.loader

    implementation(annotationProcessor(libs.mixinextras.common.get()))
}

artifacts {
    commonJava sourceSets.main.java.sourceDirectories.singleFile
    commonResources sourceSets.main.resources.sourceDirectories.singleFile
}

sourceSets.main.resources.srcDir 'src/generated/resources'

idea {
    module {
        inheritOutputDirs = false
    }
}

//import net.fabricmc.loom.task.AbstractRemapJarTask
//tasks.withType(AbstractRemapJarTask).each {
//    it.targetNamespace = "named"
//}

processResources {
    def buildProps = project.properties.clone()
    exclude("mods.groovy")

    filesMatching(['pack.mcmeta']) {
        expand buildProps
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

sourcesJar {
    from(rootProject.file("LICENSE")) {
        rename { "${it}_${mod_name}" }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.maven_group
            artifactId project.archivesBaseName
            version project.mod_version
            from components.java
            pom.withXml {
                asNode().remove(asNode().dependencies)
            }
        }
    }
}