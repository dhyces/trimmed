plugins {
    id 'idea'
    id 'java'
    id 'org.quiltmc.loom' version '1.2.+'
    id 'maven-publish'
}

archivesBaseName = "${mod_id}-common-${minecraft_version}"

loom {
    accessWidenerPath = file("src/main/resources/trimmed.accesswidener")
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-1.20.1:2023.07.02@zip")
    }

    modCompileOnly("org.quiltmc:quilt-loader:${quilt_loader_version}")

    implementation 'mysticdrew:common-networking:1.0.0+1.20-common'

    compileOnly group:'org.spongepowered', name:'mixin', version:'0.8.5'
}

idea {
    module {
        inheritOutputDirs = false
    }
}

configurations {
    testCompileOnly.extendsFrom(compileOnly)
}

import net.fabricmc.loom.task.AbstractRemapJarTask
tasks.withType(AbstractRemapJarTask).each {
    it.targetNamespace = "named"
}

processResources {
    def buildProps = project.properties.clone()

    filesMatching(['pack.mcmeta']) {

        expand buildProps
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.maven_group
            artifactId project.archivesBaseName
            version project.mod_version
            from components.java
            pom.withXml {
                asNode().remove(asNode().dependencies)
            }
        }
    }
}