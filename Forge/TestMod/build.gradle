plugins {
	alias libs.plugins.forgegradle
	alias libs.plugins.librarian
	alias libs.plugins.mixingradle
}

archivesBaseName = "${mod_id}-TESTMOD-DO-NOT-PUBLISH-forge-${libs.versions.minecraft.release}-${version}"

minecraft {
	mappings channel: "parchment", version: rootProject.ext.parchmentVersion

	accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

	runs {
		configureEach {
			workingDirectory "$rootDir/Forge/run"
			ideaModule "${rootProject.name}.Forge.${project.name}.main"
			property "mixin.config", "trimmed.mixin.json"
			property 'mixin.env.remapRefMap', 'true'
			property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

			mods {
				mod {
					source project(":Common").sourceSets.main
					source project(":Forge").sourceSets.main
				}
				testmod {
					source sourceSets.main
				}
			}
		}

		client {
			taskName 'TestClient'
		}

		server {
			taskName 'TestServer'
		}

		data {
			args '--mod', 'trimmed_testmod', 'chunked', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
			taskName 'TestData'
		}
	}
}

sourceSets.main.resources.srcDir "$rootDir/Forge/TestMod/src/generated/resources"

repositories {
	maven {
		url = "https://maven.dhyces.dev/"
	}
}

dependencies {
	minecraft libs.forge

	compileOnly(annotationProcessor(libs.mixinextras.common.get()))
	implementation(libs.mixinextras.forge)

	compileOnly(project(":Common"))
	implementation(project(":Forge"))

	annotationProcessor "${libs.sponge.mixin.get()}:processor"

//	implementation "dev.dhyces.trimmed:trimmed-forge-1.19.4:0.0.1"
}

mixin {
	config "trimmed.mixins.json"
	config "trimmed.forge.mixins.json"

	debug.verbose = true
	debug.export = true
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = 'UTF-8'
	it.options.release = 17
}

tasks.withType(GenerateModuleMetadata) {
	enabled = false
}

//tasks.withType(JavaCompile) {
//	source(project(":Common").sourceSets.main.allSource)
//	source(project(":Forge").sourceSets.main.allSource)
//}

sourceSets.each {
	def dir = layout.buildDirectory.dir("sourcesSets/$it.name")
	it.output.resourcesDir = dir
	it.java.destinationDirectory = dir
}